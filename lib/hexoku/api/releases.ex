defmodule Hexoku.API.Releases do
	alias Hexoku.Request
	@moduledoc """
	A release represents a combination of code, config vars and add-ons for an app on Heroku.

	## Attributes
	<dl>
		<dt>id</dt> <dd>unique identifier of item generated by Heroku</dd>
		<dt>description</dt> <dd>description of changes in this release</dd>
		<dt>slug</dt> <dd>slug running in this release</dd>
		<dt>slug:id</dt> <dd>unique identifier of slug</dd>
		<dt>user:email</dt> <dd>email address of account</dd>
		<dt>user:id</dt> <dd>unique identifier of user</dd>
		<dt>version</dt> <dd>unique version assigned to the release</dd>
		<dt>created_at</dt> <dd>when item was created</dd>
		<dt>updated_at</dt> <dd>when item was last modified</dd>
	</dl>

	For more info read the [Heroku API Reference](https://devcenter.heroku.com/articles/platform-api-reference#release)
	"""

	@doc """
	List existing releases.

	## Examples
		client |> Hexoku.API.Release.list("cedar")
	"""
	@spec list(Hexoku.Client.t, binary) :: [Map.t]
	def list(client, app), do: Request.get(client, "/apps/#{app}/releases")

	@doc """
	Info for existing release.

	## Examples
		client |> Hexoku.API.Release.info("myapp", "01234567-89ab-cdef-0123-456789abcdef")
	"""
	@spec info(Hexoku.Client.t, binary, binary) :: Map.t
	def info(client, app, release), do: Request.get(client, "/apps/#{app}/releases/#{release}")

	@doc """
	Create new release. The API cannot be used to create releases on Bamboo apps.

	## Examples
		client |> Hexoku.API.Release.create("myapp", "01234567-89ab-cdef-0123-456789abcdef", "Added new feature")
	"""
	@spec create(Hexoku.Client.t, binary, binary) :: Map.t
	def create(client, app, slug, description \\ "Released using Hexoku") do
		Request.post(client, "/apps/#{app}/releases", %{slug: slug, description: description})
	end

	@doc """
	Rollback to an existing release.

	## Examples
		client |> Hexoku.API.Release.rollback("myapp", "01234567-89ab-cdef-0123-456789abcdef")
	"""
	@spec rollback(Hexoku.Client.t, binary, binary) :: Map.t
	def rollback(client, app, release) do
		Request.post(client, "/apps/#{app}/releases", %{release: release})
	end

end
